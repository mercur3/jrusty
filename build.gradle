plugins {
    id "java"
    id "maven-publish"
    id "jacoco"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.2"
}

group = "com.gitlab.mercur3"
version = "1.0.2"
description = "com.gitlab.mercur3:jrusty"
java.sourceCompatibility = JavaVersion.VERSION_17

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/panic/*Exception.class",
                    "**/result/ErrorKind.class",
            ])
        }))
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
